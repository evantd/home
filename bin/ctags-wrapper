#!/usr/bin/perl -w

use strict;

use File::Copy;

my $wsRoot=`findup packageInfo`;
my $tagsFile=".tags";
if ($wsRoot) {
    $tagsFile="$wsRoot/.tags"
}
my $tmpFile = "$tagsFile.$$";

my $filename = pop(@ARGV);
$filename = $ENV{'PWD'}.'/'.$filename unless ($filename =~ m{^/});

my $tagRegex = '^[^	]*	'.$filename.'	';

my @ctagsCmd = ('ctags', '-a',);
my $ignore = undef;
for my $arg (@ARGV) {
    if ($ignore) {
        $ignore = undef;
    } elsif ($arg eq '-f') {
        push(@ctagsCmd, '-f', $tmpFile);
        $ignore = 1;
    } else {
        push(@ctagsCmd, $arg);
    }
}
push(@ctagsCmd, $filename);

# filter out any old references to this file so we don't end up with duplicates
open(my $fh, '-|', 'grep', '-v', '-e', $tagRegex, $tagsFile) || die "Failed to filter tags file: $?";
copy($fh, $tmpFile);
close($fh);

system(@ctagsCmd) == 0 or die "Failed to execute @ctagsCmd: $?";
move($tmpFile, $tagsFile);
system('grep', '-e', $tagRegex, $tagsFile) != 2 or die "Failed to grep tags file: $?";
